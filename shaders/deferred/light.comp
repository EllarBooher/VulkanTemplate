#version 460
#extension GL_EXT_buffer_reference2 : require

layout(local_size_x = 16, local_size_y = 16) in;
layout(rgba16, set = 0, binding = 0) uniform image2D image;

layout(set = 1, binding = 0) uniform sampler2D gbuffer_Diffuse;
layout(set = 1, binding = 1) uniform sampler2D gbuffer_Specular;
layout(set = 1, binding = 2) uniform sampler2D gbuffer_Normal;
layout(set = 1, binding = 3) uniform sampler2D gbuffer_WorldPosition;
layout(set = 1, binding = 4) uniform sampler2D gbuffer_OcclusionRoughnessMetallic;

layout(rg16_snorm, set = 2, binding = 0) uniform image2D randomNormals;

struct GBufferTexel
{
    vec4 position;
    vec4 normal;
    vec4 diffuseColor;
    vec4 specularColor;
    vec4 occlusionRoughnessMetallic;
};

struct PBRTexel
{
    vec3 position;
    vec3 normal;
    vec3 subscatteringColor;
    vec3 normalReflectance;
    float occlusion;
    float specularPower;
    float metallic;
};

GBufferTexel sampleGBuffer(const vec2 uv)
{
    GBufferTexel texel;
    texel.diffuseColor = texture(gbuffer_Diffuse, uv);
    texel.specularColor = texture(gbuffer_Specular, uv);
    texel.normal = texture(gbuffer_Normal, uv);
    texel.position = texture(gbuffer_WorldPosition, uv);
    texel.occlusionRoughnessMetallic = texture(gbuffer_OcclusionRoughnessMetallic, uv);

    return texel;
}

float max3(const vec3 rgb) { return max(max(rgb.r, rgb.g), rgb.b); }

PBRTexel convertPBRProperties(const GBufferTexel gbuffer)
{
    const float specularPower = 160;

    const vec3 dialectricReflectence = vec3(0.04);
    const vec3 metallicReflectence = vec3(0.5) * gbuffer.specularColor.rgb / max3(gbuffer.specularColor.rgb);

    const float metallic = gbuffer.occlusionRoughnessMetallic.b;

    return PBRTexel(
        /* position = */ gbuffer.position.xyz,
        /* normal = */ gbuffer.normal.xyz,
        /* subscatteringColor = */ gbuffer.diffuseColor.rgb,
        /* normalReflectance = */ mix(dialectricReflectence, metallicReflectence, metallic),
        /* occlusion = */ gbuffer.occlusionRoughnessMetallic.r,
        /* roughness = */ pow(specularPower, 1.0 - gbuffer.occlusionRoughnessMetallic.g),
        /* metallic = */ metallic);
}

vec3 computeFresnel(const PBRTexel material, const vec3 lightOutgoing, const vec3 viewOutgoing)
{
    const vec3 halfwayDirection = normalize(lightOutgoing + viewOutgoing);

    // Schlick approximation of fresnel reflection
    const vec3 fresnel =
        material.normalReflectance
        + (1.0 - material.normalReflectance) * pow(1.0 - clamp(dot(halfwayDirection, lightOutgoing), 0.0, 1.0), 5);

    return fresnel;
}

vec3 diffuseBRDF(const PBRTexel material, const vec3 lightOutgoing)
{
    // Lambertian BRDF

    return material.subscatteringColor / 3.14159265359;
}

vec3 specularBRDF(const PBRTexel material, const vec3 lightOutgoing, const vec3 viewOutgoing)
{
    const vec3 halfwayDirection = normalize(lightOutgoing + viewOutgoing);

    const float specularPower = material.specularPower;
    const float microfacetDistribution = pow(clamp(dot(halfwayDirection, material.normal), 0.0, 1.0), specularPower);

    // Without this term, the overall brightness decreases as roughness increases
    const float normalizationTerm = (specularPower + 2) / 8.0;

    return vec3(normalizationTerm * microfacetDistribution);
    //return vec3(100.0 * clamp(dot(lightOutgoing, material.normal), 0.0, 1.0));
}

layout(push_constant) uniform PushConstant
{
    vec2 offset;
    vec2 gBufferCapacity;

    vec4 cameraPosition;

    vec4 lightForward;

    mat4 cameraProjView;

    vec2 extent;

    float occluderRadius;
    float occluderBias;
    float aoScale;
    float lightStrength;

    float ambientStrength;
    bool gbufferWhiteOverride;
    vec2 padding0;
} pc;

layout(constant_id = 0) const bool enableAO = false;
layout(constant_id = 1) const bool enableRandomNormalSampling = false;
layout(constant_id = 2) const bool normalizeRandomNormals = false;

vec3 computeLightContribution(const vec3 lightForward, const PBRTexel material, const vec3 viewDirection)
{
    // Our model uses a microfacet specular BRDF layered with a diffuse BRDF
    // The assumption is that all diffuse light is scattered from whatever is not reflected as a specular highlight

    const vec3 lightDirection = normalize(-lightForward);

    const vec3 diffuseContribution = diffuseBRDF(material, lightDirection);
    const vec3 specularContribution = specularBRDF(material, lightDirection, viewDirection);

    const vec3 fresnel = computeFresnel(material, lightDirection, viewDirection);

    // Lerping preserves the energy conservation of our BRDFs
    return pc.lightStrength * material.occlusion * mix(diffuseContribution, specularContribution, fresnel)
         * clamp(dot(material.normal, lightDirection), 0.0, 1.0);
}

vec2 sampleRandomNormal(vec2 uv, float scale)
{
    const ivec2 texelCoord = ivec2(mod(uv * scale, 1.0) * imageSize(randomNormals));

    // Load only xy, since we just need to reflect in 2D screen space
    vec2 sampledNormal = imageLoad(randomNormals, texelCoord).xy;
    if(normalizeRandomNormals)
    {
        sampledNormal = normalize(sampledNormal);
    }

    return sampledNormal;
}

float sampleOcclusion(const GBufferTexel occludee, const vec2 uv)
{
    const vec3 occluderPosition = texture(gbuffer_WorldPosition, uv).xyz;
            
    // Avoid sampling gbuffer out of bounds
    const vec2 uvMin = pc.offset / pc.gBufferCapacity;
    const vec2 uvMax = (pc.offset + pc.extent) / pc.gBufferCapacity;
    if (uv.x <= uvMin.x || uv.y <= uvMin.y || uv.x >= uvMax.x || uv.y >= uvMax.y)
    {
        return 0.0;
    }

    if(texture(gbuffer_Diffuse, uv).a < 1.0)
    {
        return 0.0;
    }

    const vec3 v = occluderPosition - occludee.position.xyz;
    const float d = length(v);

    if(d < 0.000001)
    {
        // TODO: analyze what percent of samples hit this case
        return 0.0;
    }

    // The sphere occluder subtends a solid angle from the perspective of the occludee
    const float occluderSolidAngle = 6.28318530718 * (1.0 - d / sqrt(d * d + pc.occluderRadius * pc.occluderRadius));

    // Normally we need to integrate the occluded light over all v towards the sphere, but we can assume v does not
    // vary much so we multiply by a single attenuation factor.
    return clamp(dot(occludee.normal.xyz, normalize(v)) - pc.occluderBias, 0.0, 1.0) * occluderSolidAngle;
}

float computeAmbientOcclusionAttenuation(GBufferTexel occludee, vec2 gbufferUV, vec3 cameraPosition)
{
    if (!enableAO)
    {
        return 1.0;
    }

    float accumulatedOcclusion = 0;

    const float distanceToCamera = distance(cameraPosition, occludee.position.xyz);
    const float uvSampleRadius = pc.aoScale / distanceToCamera;

    // Rotation by 45 degrees
    const mat2 sampleRotate = mat2(
        0.707, -0.707,
        0.707, 0.707
    );

    // 4 cardinal directions
    const vec2 sampleOffsets[4] = vec2[4](
        vec2(-1.0, 0), 
        vec2(0, 1.0), 
        vec2(1.0, 0), 
        vec2(0, -1.0)
    );

    // Our sampling strategy is 4 sets of 4, distributed evenly across all angles
    // Slight density increase towards the center.
    for(int i = 0; i < 4; i++)
    {
        vec2 offset1 = sampleOffsets[i];

        if (enableRandomNormalSampling)
        {
            offset1 = reflect(offset1, sampleRandomNormal(gbufferUV, 100.0));
        }

        // Scale after rotating just in case scaling is nonuniform
        const vec2 offset2 = uvSampleRadius * sampleRotate * offset1;
        offset1 = uvSampleRadius * offset1;

        accumulatedOcclusion += sampleOcclusion(occludee, gbufferUV + 0.25 * offset1);
        accumulatedOcclusion += sampleOcclusion(occludee, gbufferUV + 0.75 * offset1);

        accumulatedOcclusion += sampleOcclusion(occludee, gbufferUV + 0.5 * offset2);
        accumulatedOcclusion += sampleOcclusion(occludee, gbufferUV + 1.0 * offset2);
    }

    return clamp(1.0 - accumulatedOcclusion / (4.0 * 4.0 * 6.28318530718), 0.0, 1.0);
}

void main()
{
    const vec2 size = imageSize(image);
    const ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy + pc.offset);
    if (texelCoord.x >= size.x || texelCoord.y >= size.y)
    {
        return;
    }

    // The uv needs to be offset to avoid floating point errors on texel boundaries
    const vec2 offset = vec2(0.5, 0.5);
    const vec2 gbufferUV = (vec2(texelCoord) + offset) / pc.gBufferCapacity;

    GBufferTexel gbufferTexel = sampleGBuffer(gbufferUV);

    // No transparent geometry for now, less than 1.0 alpha indicates background texels
    if (gbufferTexel.diffuseColor.a < 1.0)
    {
        return;
    }
    if (pc.gbufferWhiteOverride)
    {
        gbufferTexel.diffuseColor = vec4(1.0);
        gbufferTexel.specularColor = vec4(1.0);
    }

    const PBRTexel material = convertPBRProperties(gbufferTexel);

    const vec3 viewDirection = normalize(pc.cameraPosition.xyz - material.position.xyz);

    // Slightly offset light
    const vec3 lightForward = normalize(pc.lightForward.xyz);
    const vec3 lightContribution = computeLightContribution(lightForward, material, viewDirection);

    const vec3 ambientContribution = vec3(pc.ambientStrength) * material.subscatteringColor * computeAmbientOcclusionAttenuation(gbufferTexel, gbufferUV, pc.cameraPosition.xyz);

    const vec3 totalLight = lightContribution + ambientContribution;
    imageStore(image, texelCoord, vec4(totalLight, 1.0));
}